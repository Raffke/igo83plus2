;****************************************************************************************************************
;!Matze - added variable for multiroute
<header>
	<var saved_vRouteType>
	<var saved_RouteCorrectness>
</header>

<state st_BrowseMap>
	<uselayer map/>
;!Matze - Night color filter
	<uselayer ui_NavigateMapnight/>

	<script init>
		;THERE ISN'T BROWSEMAP IN iGO8
	</script>

	<script done>
	</script>
</state>


<script sc_btnBMC_Compass_OnRelease>
	obs_OverviewMode_Changed.stop

	run sc_NaviMenuAlphaRestore
	vRotationAngle.set %map.rotation_angle
	map.GetMapState vMapState
	runif vMapState 0 sc_2DHeadUpCheck
	else_runif vMapState 3 'map.SetMapState 4, vMapState.set 4, run sc_2DMode'
	else_runif vMapState 4 'map.SetMapState 0, vMapState.set 0, run sc_3DMode, run sc_3dMapFollowMode'
	else_runif vMapState 5 'run sc_Back_From_Overview'
	else_run 'run sc_3DMode'
	map.GetMapState vMapState

;!Matze - replaced map.SETCARPOSITION by call script
	runif vMapState 3 'run sc_Car_Pos1'
	runif vMapState 4 'run sc_Car_Pos2'

	obs_OverviewMode_Changed.start "NO_TRIGGER"
	
;!Matze 
	runif bShowCompassOnMap 0 sc_north_lock
	else_run sc_north_lock1	
;	btnNMB_Nav3D.isvisible vTmp
;	runif vTmp  1 '.map.north_lock 1'
;	else_run '.map.north_lock 0'
</script>

<script sc_3dMapFollowMode>
    runif %map.follow_gps 0 '.map.follow_rotation 1, .map.follow 0'
    else_run '.map.follow_rotation 1, .map.follow 1'
</script>

<script sc_Back_From_Overview>
	map.GET_MAPSTATE_BEFORE_OVERVIEW vTmp
	map.setmapstate vTmp
	.map.zoom_level vZoomLevelBeforeOverview
	vMapState.set vTmp
	runif vTmp 0 'run sc_ExitOverviewTo3D'
	else_runif vTmp 3 'run sc_ExitOverviewTo2D'
	else_runif vTmp 4 'run sc_ExitOverviewTo2D'
;!Matze - added for advanced overview settings 
	vSaveOverviewMode.SET 3
	run sc_LaneInfoShow_Check
</script>

<script sc_2DHeadUpCheck>
	runif v2DHeadUp 1 sc_2DHeadUpEnabled
	else_run 'map.SetMapState 4, run sc_2DMode'
</script>

<script sc_2DHeadUpEnabled>
	map.SetMapState 3
	map.mapmode 0
	run sc_2DHeadUpInit
	run sc_2DMode
	.map.rotation_angle vRotationAngle
</script>


;****************************************************************************************************************

<state st_PlanMode>
	<uselayer map/>
	<uselayer ui_Title/>
	<uselayer ui_PlanMode/>
	<uselayer ui_PlanModeControls2D/>
	<uselayer ui_PlanModeOpenCloseButtons/>
;!Matze - Night color filter
	<uselayer ui_NavigateMapnight/>
;!TB - Popup Info
	<uselayer ui_CockpitInfo>

	<script init>
		Title.set ""
		run sc_FullScreenMap

		map.mapmode 1
		vMapSettings2d3d.set "2D"
		map.VIEW2D3D vMapSettings2d3d

;!Matze - added popup info
		runifnot vPopUpShowPanel 0 'run sc_ShowCockpitInfo'
		else_run 'run sc_HideCockpitInfo'

		map.EnableMoveCursorByClick 1
		map.SHOWCURSOR 1
		map.ONMAPCLICK sc_StartPlanModeMenuPlan_Refresh 1	

		vPlanModeOn.set 1

		run sc_init_zoomcontrol_planmode

		run sc_FitToScreen_PlanMode

;!Matze - added for multiroute algorithm
		saved_RouteCorrectness.set %route.correctness
;!Raffke_jr - moved here for permanent update of Route Info button
		vRouteCalcFinishedCallback.set sc_PM_RefreshRouteInfo
		run sc_PM_RefreshRouteInfo
;!Raffke_jr - TODO: is it possible to display "..." if infos invalid (i.e. called from 'Load Route')?

		btnStartPoint.DISABLE
		runif %navigation.waypoints.list.size > 1 'btnStartPoint.ENABLE'

		runif bBackupNavMapFollowPlanMode 1 'run sc_FollowOff'

		run sc_StartPlanModeMenuPlan

		; any button can be used for this. This must be after all show/hide layers.
		btn_Footer_back2.CLEARFOCUS
	</script>

	<script done>	
		vPlanModeOn.set 0	
;!Raffke_jr - moved here for permanent update of Route Info button
		vRouteCalcFinishedCallback.clear
;!Matze - Popup Info
		run sc_HideCockpitInfo

;!Matze - added for multiroute algorithm
		.route.correctness saved_RouteCorrectness
		run sc_MultiplanCancel
	</script>
</state>

<script sc_StartPlanModeMenuPlan>
	btnStartPoint.ENABLE
	btnRemStartPoint.ENABLE
	btnDestPoint.ENABLE
	btnRemDestPoint.ENABLE
	btnAddViaPoint.ENABLE
	btnRemViaPoint.ENABLE
	btnContinue.ENABLE
	
	GETPLACEROUTEMODE vTmp
	runif vTmp 1 'btnStartPoint.HIDE, btnRemStartPoint.SHOW'
	else_run 'btnStartPoint.SHOW, btnRemStartPoint.HIDE'
	runif vTmp 2 'btnAddViaPoint.HIDE, btnRemViaPoint.SHOW'
	else_run 'btnAddViaPoint.SHOW, btnRemViaPoint.HIDE'
	runif vTmp 3 'btnDestPoint.HIDE, btnRemDestPoint.SHOW'
	else_run 'btnDestPoint.SHOW, btnRemDestPoint.HIDE'
	runif vTmp 5 'btnAddViaPoint.DISABLE, btnContinue.DISABLE'
	else_run 'btnPMO_AddVia.ENABLE, btnPMO_Continue.ENABLE'
	
	runif vTmp 1 'btnDestPoint.DISABLE, btnRemDestPoint.DISABLE, btnAddViaPoint.DISABLE, btnRemViaPoint.DISABLE, btnContinue.DISABLE'
	else_runif vTmp 2 'btnStartPoint.DISABLE, btnRemStartPoint.DISABLE, btnDestPoint.DISABLE, btnRemDestPoint.DISABLE, btnContinue.DISABLE'
	else_runif vTmp 3 'btnStartPoint.DISABLE, btnRemStartPoint.DISABLE, btnContinue.DISABLE,btnAddViaPoint.DISABLE, btnRemViaPoint.DISABLE'
</script>

<script sc_StartPlanModeMenuPlan_Refresh>
;!TB - Popup Info
	run sc_DoStartHideCockpitInfo
;!Matze - added popup info
	runifnot vPopUpShowPanel 0 'run sc_ShowCockpitInfo'
	else_run 'run sc_HideCockpitInfo'

	5 run sc_StartPlanModeMenuPlan
;!Matze - added to show StartNav- or MultiCancel-Button
	5 runif vMultiRouteCalculate 1 'btnPM_StartNav.hide, btnPM_MultiCancel.show'
	else_run 'run sc_StartPlanModeMenuPlan, btnPM_StartNav.show, btnPM_MultiCancel.hide'	
</script>

<script sc_FitToScreen_PlanMode>
	map.MOVECURSORTOSELECTEDITEM 0
	runif %navigation.waypoints.list.size > 1 'map.SETCARPOSITION 50 50, run sc_Fittoscreen_res'
	else_run 'map.CENTERCURSOR'
	2 run sc_StartPlanModeMenuPlan_Refresh
</script>


<script sc_StartPlanMode>
	bBackupNavMapFollowPlanMode.SET bNavMapFollow

;!TB - fix
	map.SETCARPOSITION 50 50

;!Raffke_jr - avoid loops
	CLEARTOSTATE st_PlanMode
	NEXTSTATE st_PlanMode
</script>
 
<script sc_btnPM_StartNavigation>
	run sc_FollowOn
	map.mapmode 0
	run sc_EmptyStateQueue st_NavigateMap
</script>


;!TB - Added script for "Center Cursor" button on the 2D map
<script sc_btnPMB_FitToCursor_PlanMode>
	map.MOVECURSORTOSELECTEDITEM 0
	map.CENTERCURSOR
	2 run sc_StartPlanModeMenuPlan_Refresh
</script>

;!Matze - added script for "Center GPS Position" button on the 2D map
<script sc_btnPMB_FitToGPS_PlanMode>
	map.MOVECURSORTOGPS
	map.CENTERCURSOR
	2 run sc_StartPlanModeMenuPlan_Refresh
</script>

;****************************************************************************************************************
;!TB NGP - ReCalc Route
;!Raffke_jr - moved here from common.ui, changed to easier implementation from route.ui

<script sc_PM_CycleRouteMethods>
	termif nVehicle 5 sc_CycleRouteMethods
	termif nVehicle 4 sc_CycleRouteMethods
	termif nVehicle 6 sc_CycleRouteMethods
	vTmp.TEXTMODEL_WSTR "route.current_route_type"
	vTmp.DROPMODEL
	runif vTmp L"FAST" 'vTmp.set L"ECO"'
	else_runif vTmp L"ECO" 'vTmp.set L"SIMPLE"'
	else_runif vTmp L"SIMPLE" 'vTmp.set L"SHORT"'
	else_runif vTmp L"SHORT" 'vTmp.set L"SCENIC"'
	else_runif vTmp L"SCENIC" 'vTmp.set L"FAST"'
	vRouteType.set vTmp

	run sc_PM_RefreshRouteMethod
;!Raffke_jr - replace default "Calculating..", which is too long here
	txtPM_Distance.text "..."
	txtPM_TimeLeft.text " "

	ADVNAV_RECALC
</script>

<script sc_btn_PM_Method_OnRelease>
	term sc_PM_CycleRouteMethods
	1 run sc_PM_CycleRouteMethods
</script>

<script sc_PM_RefreshRouteMethod>
;!Matze - color added
	runif vRouteType L"FAST" 'txtPM_Method.text "Fastest", txtPM_Method.color #FF0000'
	else_runif vRouteType L"SHORT" 'txtPM_Method.text "Shortest", txtPM_Method.color #00FF00'
	else_runif vRouteType L"ECO" 'txtPM_Method.text "Economic", txtPM_Method.color #0000FF'
	else_runif vRouteType L"SIMPLE" 'txtPM_Method.text "Easy", txtPM_Method.color #ffff00'
	else_runif vRouteType L"SCENIC" 'txtPM_Method.text "Scenic/Panoramic_View", txtPM_Method.color #00FFFF'
</script>

<script sc_PM_RefreshRouteInfo>
	run sc_PM_RefreshRouteMethod

	ui_Route.GET_ROUTEINFO txtPM_Distance txtRI_Stops txtPM_TimeLeft txtRI_Arrive txtRI_DestAddress
;!Raffke_jr - Fit to screen after recalc
	run sc_FitToScreen_PlanMode
</script>

;****************************************************************************************************************
;!Matze - added new scripts for multiroute

<script sc_MultiplanSelect>
	FINDPATH_SELECT_MULTIPLAN $1

	run sc_MultiplanCancel
	runif vRouteType saved_vRouteType 'run sc_Fittoscreen_res'
	else_run 'run sc_RMchange'
</script>

<script sc_MultiplanCancel>
	vMultiRouteCalculate.set 0
	ui_MultiRouteDrawProgress.hide
	ui_MultiRouteDrawProgress_min.hide

	.route.multiplan_draw_all 0
	ui_SelectRM_Algorithm.hide
	run sc_setPlanmode
</script>

<script sc_setMultiroute>
	run sc_init_zoomcontrol_planmode
	ui_PlanModeOpenCloseButtons.hide
	ui_PlanModeMultirouteButtons.show
	btnPM_StartNav.hide
	btnPM_MultiCancel.show
</script>

<script sc_setPlanmode>
	run sc_init_zoomcontrol_planmode
	ui_PlanModeOpenCloseButtons.show
	ui_PlanModeMultirouteButtons.hide
	btnPM_MultiCancel.hide
	btnPM_StartNav.show
</script>

<Script sc_RMchange>
	ADVNAV_RECALC
	runif vActiveState "st_planmode" 'run sc_FitToScreen_PlanMode, run sc_PM_RefreshRouteMethod'
	else_run sc_RefreshRouteInfo
</script>

<script sc_RMselect>
	run sc_MultiplanCancel
	ui_SelectRM.show
	saved_vRouteType.set vRouteType
	runif vRouteType L"FAST" 'nRMTmp.set "0"'
	runif vRouteType L"SHORT" 'nRMTmp.set "1"'
	runif vRouteType L"ECO" 'nRMTmp.set "2"'
	runif vRouteType L"SIMPLE" 'nRMTmp.set "3"'
	runif vRouteType L"SCENIC" 'nRMTmp.set "4"'
</script>


<script sc_RMselected>
	ui_SelectRM.hide
	runif nRMTmp "0" 'vRouteType.set L"FAST"'
	runif nRMTmp "1" 'vRouteType.set L"SHORT"'
	runif nRMTmp "2" 'vRouteType.set L"ECO"'
	runif nRMTmp "3" 'vRouteType.set L"SIMPLE"'
	runif nRMTmp "4" 'vRouteType.set L"SCENIC"'

	runifnot saved_vRouteType vRouteType  sc_RMchange
</script>

<script sc_MultiRouteCalculateCheck>
;!Raffke_jr - only go on, if no other UI is being visible
	ui_SelectRM.isvisible vTmp
	runifnot vTmp 1 'ui_SelectRM_Algorithm.isvisible vTmp'
	runif vTmp 1 ''
	else_runif vMultiRouteCalculate 0 'run sc_RMselect'
	else_run 'ui_SelectRM_Algorithm.show'
</script>

<script sc_MultirouteCheck>
	runif vMultiRouteCalculate 0 'PREVSTATE'
	termif vMultiRouteCalculate 0
	vMultiRouteCalculate.set 0
;!Raffke_jr - find workaround for obsolete variable
	vPlanModeFromMultiRoute.set 0
	FINDPATH_MULTIPLAN_CANCEL
	PREVSTATE
</script>

<script sc_MultirouteCheck1>
	runif vPlanModeFromMultiRoute 1 sc_return_Multiroute
	else_run sc_MultirouteCheck
</script>

<script sc_return_Multiroute>
	runif vMultiRouteCalculate 1 '.route.multiplan_draw_all 0, vPlanModeFromMultiRoute.SET 0, prevstate'
	else_run '.route.multiplan_draw_all 0, vPlanModeFromMultiRoute.SET 0, prevstate, prevstate'
;!Matze - possible variant (not used)
;	else_run sc_return_Multiroute1
	vMultiRouteCalculate.set 0
</script>

;!Matze - possible variant (not used)
;<script sc_return_Multiroute1>
;	FINDPATH_SET_MULTIPLAN_METHODS L"FAST" L"SHORT" L"ECO" L"SIMPLE" L"SCENIC"
;	FINDPATH_SELECT_MULTIPLAN_DRAW 0
;	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute1 1
;	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute2 2
;	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute3 3
;	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute4 4
;	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute5 5
;	FINDPATH_MULTIPLAN_RECALC
;	PREVSTATE
;</script>

<script sc_MultiRouteDraw>
	FINDPATH_MULTIPLAN_CANCEL
	vMultiRouteCalculate.set 1
	.route.multiplan_draw_all 1
	ui_MultiRouteDrawProgress_min.hide
	ui_MultiRouteDrawProgress.show
	FINDPATH_SET_MULTIPLAN_METHODS L"FAST" L"SHORT"  L"ECO" L"SIMPLE" L"SCENIC"
	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute11 1
	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute22 2
	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute33 3
	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute44 4
	ui_route.ATTACH_ROUTE_SPRITE_PROVIDER sprMPRoute55 5
	FINDPATH_MULTIPLAN_RECALC
	run sc_Fittoscreen_res
	.route.correctness saved_RouteCorrectness
</script>
